buildscript {
	ext.kotlin_version = '1.0.3'

	repositories {
		jcenter()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath 'com.android.tools.build:gradle:2.1.0'
		classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
	}
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'


android {
	compileSdkVersion 23
	buildToolsVersion "23.0.2"
	defaultConfig {
		applicationId "org.kbieron.iomerge"
		minSdkVersion 19
		targetSdkVersion 23
		versionCode 1
		versionName "1.0"
	}
	/**
	 * $GRADLE_HOME/gradle.properties:
	 *                                 RELEASE_STORE_FILE=/path/to/keystore.jks
	 *                                 RELEASE_STORE_PASSWORD=*
	 *                                 RELEASE_KEY_ALIAS=*
	 *                                 RELEASE_KEY_PASSWORD=*
	 */
	signingConfigs {
		release {
			storeFile file(RELEASE_STORE_FILE)
			storePassword RELEASE_STORE_PASSWORD
			keyAlias RELEASE_KEY_ALIAS
			keyPassword RELEASE_KEY_PASSWORD
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			zipAlignEnabled true
			signingConfig signingConfigs.release
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	lintOptions {
		disable 'InvalidPackage'
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
	}
}

dependencies {
	compile project(':ndkModule')

	compile 'com.github.Krzychek.iomerge-server:data-model:master-SNAPSHOT'

	// kotlin libs
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	compile 'com.pawegio.kandroid:kandroid:0.6.12@aar'
	compile 'com.github.JakeWharton:kotterknife:-SNAPSHOT'
	compile 'com.chibatching:kotpref:1.4.0'

	// com.android.support
	compile 'com.android.support:appcompat-v7:23.4.+'
	compile 'com.android.support:design:23.4.+'
	compile 'com.android.support:cardview-v7:23.4.+'
	compile 'com.android.support:support-v4:23.4.+'

	// leak canary
	debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
	releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
	testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
}
